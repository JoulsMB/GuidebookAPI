import net.darkhax.curseforgegradle.TaskPublishCurseForge
import net.darkhax.curseforgegradle.Constants as CFG_Constants

plugins {
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply false
    id 'fabric-loom' version '1.6-SNAPSHOT' apply false

    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'net.darkhax.curseforgegradle' version '1.0.8'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.neoforged.gradle.userdev' version '7.0.120'
}

base.archivesName = "${mod_id}-${mc_version}-${mod_version}-neoforge"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar ()
}

project.evaluationDependsOn(":CommonApi")

var notNeoTask = { Task task -> !task.name.startsWith("neo") }
tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source project(":CommonApi").sourceSets.main.getAllSource ()
}

sourceSets {
    main {
        compileClasspath += project(":CommonApi").sourceSets.main.output
        runtimeClasspath += project(":CommonApi").sourceSets.main.output
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    runtimeOnly project(":CommonApi")
}

runs {
    create("client").configure {
        systemProperty "forge.logging.console.level", "debug"
        workingDirectory file("run/client")

        modSources(
                [sourceSets.main]
        )
        dependencies {
            runtime project(":CommonApi")
        }
    }
    create("server").configure {
        systemProperty "forge.logging.console.level", "debug"
        workingDirectory file("run/server")

        programArgument "--nogui"

        modSources(
                [sourceSets.main]
        )
        dependencies {
            runtime project(":CommonApi")
        }
    }
}

subsystems {
    parchment {
        minecraftVersion("${parchment_mc_version}")
        mappingsVersion("${parchmentVersion}")
    }
}

tasks.register("publishCurseforge", TaskPublishCurseForge) {
    dependsOn(tasks.jar)

    apiToken = project.findProperty("curseforgeApiToken") ?: "0"

    var mainFile = upload(curseProjectId, tasks.jar.get().archiveFile)
    mainFile.changelogType = CFG_Constants.CHANGELOG_MARKDOWN
    mainFile.changelog = file("../changelog/CHANGELOG.MD")
    mainFile.releaseType = CFG_Constants.RELEASE_TYPE_RELEASE
    mainFile.addJavaVerson("Java 21")
    mainFile.addGameVersion("${mc_version}")
    mainFile.addModLoader("NeoForge")
}