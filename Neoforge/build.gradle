plugins {
    id 'net.neoforged.gradle.vanilla' version '7.0.107' apply false
    id 'fabric-loom' version '1.6-SNAPSHOT' apply false
    id 'net.neoforged.gradle.userdev' version '7.0.107'

    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

sourceSets {
    main {
        compileClasspath += project(":CommonApi").sourceSets.main.output
        runtimeClasspath += project(":CommonApi").sourceSets.main.output
    }
}

var notNeoTask = { Task task -> !task.name.startsWith("neo") }

tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source project(":CommonApi").sourceSets.main.getAllSource ()
}

dependencies  {
    implementation "net.neoforged:neoforge:${neo_version}"
    runtimeOnly project(":CommonApi")
}

runs {
    client {
        systemProperty('forge.logging.console.level', 'debug')
        systemProperty('forge.enableGameTestNamespaces', "${project.mod_id}")

        modSources(
                [sourceSets.main,
                 project(":CommonApi").sourceSets.main]
        )
    }
}

subsystems {
    parchment {
        minecraftVersion = "${parchment_mc_version}"
        mappingsVersion = "${parchmentVersion}"
    }
}

curseforge {
    apiKey = "${curseforgeApiToken}"
    project {
        id = "${curseProjectId}"
        changelog = ""
        releaseType = "release"
        addGameVersion "${mc_version}"

        mainArtifact(jar) {
            displayName = "GuidebookApi-Neoforge-${mod_version}"
        }
    }
}