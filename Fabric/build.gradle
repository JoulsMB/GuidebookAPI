plugins {
    id 'java-library'
    id 'fabric-loom' version '1.6-SNAPSHOT'

     id "com.modrinth.minotaur" version "2.+"
}

group = 'de.wonejo.gapi'
version = "${mc_version}-${mod_version}-fabric"
base.archivesName = "${mod_id}-${mc_version}-${mod_version}-fabric"

java {
    withSourcesJar()

    toolchain.languageVersion = JavaLanguageVersion.of(21)
    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    main {
        resources.srcDirs += project(":Common").sourceSets.main.resources.srcDirs
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.officialMojangMappings ()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    compileOnly project(":Common")
}

loom {
    runs {
        named("client") {
            client()
            configName = "FabricClient"
            ideConfigGenerated = true
            runDir("run/client")
            programArg '--username=Dev'
        }
        named("server") {
            server()
            configName = "FabricServer"
            ideConfigGenerated = true
            runDir("run/server")
            programArg '--nogui'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

modrinth {
    token = project.findProperty("modrinth_token")
    projectId = project.findProperty("modrinth_project_id")
    uploadFile = remapJar
    gameVersions = ["1.20.6"]
    loaders = ['fabric']
    dependencies {
        required.project 'fabric-api'
    }
}