plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'me.shedaniel.unified-publishing' version '0.1.+'
}
//
group = "${mod_group}"
version = "${mod_version}"
base {
    archivesName = "${mod_id}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

loom {
    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        client {
            configName = "MinecraftClient"
            client()
        }
    }

}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.2:2023.10.08@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation project(":CommonAPI")
}

unifiedPublishing {
    project {
        displayName = 'guidebookapi-fabric'
        version = "${mod_version}"
        releaseType = 'release'
        gameVersions = ['1.20.2']
        gameLoaders = ['fabric']

        curseforge {
            token = project.curseforgeToken
            id = project.curseforgeProjectId

            displayName = "guidebookapi-fabric-${mod_version}"
            version = "${mod_version}"
            changelog = "Added configurations for the mod and fix some bugs."
            releaseType = 'release'
            gameVersions = ['1.20.2']
            gameLoaders = ['fabric']

            mainPublication tasks.jar
        }
    }
}

tasks.named("jar", Jar).configure {
    from project(":CommonAPI").sourceSets.main.output

    manifest {
        attributes([
                'Specification-Title': "${mod_id}",
                'Specification-Vendor': "${mod_authors}",
                'Specification-Version': 1,
                'Implementation-Title': "${mod_id}-fabric",
                'Implementation-Version': "${mod_version}",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}