import net.darkhax.curseforgegradle.Constants
import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply false
    id 'net.neoforged.gradle.userdev' version '7.0.120' apply false

    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'net.darkhax.curseforgegradle' version '1.0.8'
    id 'com.modrinth.minotaur' version '2.+'
    id 'fabric-loom' version '1.6-SNAPSHOT'
}

base.archivesName = "${mod_id}-${mc_version}-${mod_version}-fabric"

project.evaluationDependsOn(":CommonApi")

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar ()
}

repositories {
    maven {
        url "https://maven.parchmentmc.org"
        content {
            includeGroupByRegex("org\\.parchmentmc.*")
        }
    }
}


dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.layered {
        officialMojangMappings ()
        parchment ("org.parchmentmc.data:parchment-${parchment_mc_version}:${parchmentVersion}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    runtimeOnly project(":CommonApi")
}

loom {
    runs {
        named("client") {
            client ()
            configName = "Fabric Client"
            ideConfigGenerated true
            runDir ("run/client")
        }
        named('server') {
            server ()
            configName = "Fabric Server"
            ideConfigGenerated = true
            runDir ("run/server")
            vmArgs ("--nogui")
        }
    }
}

tasks.register("publishCurseforge", TaskPublishCurseForge) {
    dependsOn(tasks.remapJar)

    apiToken = project.findProperty("curseforgeApiToken") ?: "0"

    var mainFile = upload(curseProjectId, tasks.jar.get().archiveFile)
    mainFile.changelogType = Constants.CHANGELOG_MARKDOWN
    mainFile.changelog = file("../changelog/CHANGELOG.MD")
    mainFile.releaseType = Constants.RELEASE_TYPE_RELEASE
    mainFile.addJavaVerson("Java 21")
    mainFile.addGameVersion("${mc_version}")
    mainFile.addModLoader("Fabric")
    mainFile.addRequirement("fabric-api")
}